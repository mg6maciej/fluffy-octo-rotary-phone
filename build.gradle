buildscript {
    ext.kotlin_version = '1.1.0'
    ext.support_version = '25.2.0'
    ext.retrofit_version = '2.2.0'
    ext.google_deps_version = '9.8.0'
    ext.mockito_version = '2.7.16'
    ext.mockito_kotlin_version = '1.3.0'
    ext.leak_canary_version = '1.5'
    ext.el_android_commons_version = '0.0.12'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.14.0'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    configurations.all {
        resolutionStrategy {
            forcedModules = [
                    "com.android.support:support-annotations:$support_version",
                    "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'com.github.ben-manes.versions'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
