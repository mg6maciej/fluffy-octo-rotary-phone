buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.5.0-x'
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco-everywhere'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "pl.mg6.likeornot"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1001001
        versionName "1.1.1-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            ext.betaDistributionGroupAliases = 'testers'
            ext.betaDistributionReleaseNotesFilePath = "crashlytics_release_notes.txt"
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/testCommon/java'
        androidTest.java.srcDirs += 'src/testCommon/java'
    }
    dexOptions {
        javaMaxHeapSize DEX_MAX_HEAP_SIZE
        preDexLibraries = System.env.CI != 'true'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.2.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.firebase:firebase-core:9.8.0'
    compile 'com.google.firebase:firebase-crash:9.8.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.elpassion.android.commons:espresso:0.0.12') {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }
    androidTestCompile 'org.mockito:mockito-android:2.7.13'
    androidTestCompile 'com.nhaarman:mockito-kotlin:1.3.0'
    androidTestCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile 'com.nhaarman:mockito-kotlin:1.3.0'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

afterEvaluate {
    if (FABRIC_API_SECRET) {
        ant.propertyfile(file: "fabric.properties") {
            entry(key: "apiSecret", value: FABRIC_API_SECRET)
        }
    }
}
