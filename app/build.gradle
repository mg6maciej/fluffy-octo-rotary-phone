buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.trickyandroid:jacoco-everywhere:0.2.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.5.0-x'
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco-everywhere'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "pl.mg6.likeornot"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1001001
        versionName "1.1.1-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            ext.betaDistributionGroupAliases = 'testers'
            ext.betaDistributionReleaseNotesFilePath = "crashlytics_release_notes.txt"
        }
    }
    sourceSets {
        test.java.srcDirs += 'src/testCommon/java'
        androidTest.java.srcDirs += 'src/testCommon/java'
    }
    dexOptions {
        javaMaxHeapSize DEX_MAX_HEAP_SIZE
        preDexLibraries = System.env.CI != 'true'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:appcompat-v7:$support_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.0'
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile "com.google.firebase:firebase-core:$google_deps_version"
    compile "com.google.firebase:firebase-crash:$google_deps_version"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leak_canary_version"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leak_canary_version"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile("com.elpassion.android.commons:espresso:$el_android_commons_version") {
        exclude group: 'com.android.support'
    }
    androidTestCompile "org.mockito:mockito-android:$mockito_version"
    androidTestCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile "com.nhaarman:mockito-kotlin:$mockito_kotlin_version"
}

afterEvaluate {
    if (FABRIC_API_SECRET) {
        ant.propertyfile(file: "fabric.properties") {
            entry(key: "apiSecret", value: FABRIC_API_SECRET)
        }
    }
}
